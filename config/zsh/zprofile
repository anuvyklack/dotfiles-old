# Executes commands at login pre-zshrc.

# For WSL
[[ "$(umask)" = "000" ]] && umask 022


# Editors
# ========================================================================

export EDITOR VISUAL
if command -v nvim > /dev/null 2>&1; then
  EDITOR='nvim'
else
  EDITOR='vim'
fi
VISUAL="$EDITOR"

export PAGER='less'


# Language
# ========================================================================

if [[ -z "$LANG" ]]; then
    export LANG='en_US.UTF-8'
    export LANGUAGE=en_US.UTF-8
fi


# Paths
# ========================================================================

# Broot {{{
if [[ -s "~/.config/broot/launcher/bash/br" ]]; then
    source ~/.config/broot/launcher/bash/br
fi
# }}}

# Homebrew {{{
if [[ -a "/home/linuxbrew/.linuxbrew/bin/brew" ]]
then
    eval $(/home/linuxbrew/.linuxbrew/bin/brew shellenv)

    # Enable completion for brew and programs installes with brew.
    FPATH=$(brew --prefix)/share/zsh/site-functions:$FPATH
fi
# }}}

# Put Miniconda after Homebrew to make miniconda python be earlier in the $PATH.
# Miniconda {{{

# if is cache load cache
# elsif no cache make cache
# fi

setopt LOCAL_OPTIONS EXTENDED_GLOB GLOB_DOTS

local CACHE="$HOME/.cache/zsh/conda.zsh"

# Существует файл, который был модифицирован в течении 7 последних дней:
# -----  ---------------------------------------------
# (#q…)  glob qualifier
#     .  regular file
#  md-7  was modified within last 7 days
#     N  don't raise an error if nothing will be found
# -----  ---------------------------------------------
#
if [[ -s $CACHE(#q.md-7N) ]]
then
    source $CACHE
else
    local PATH_TO_CONDA_INSTALL='/opt/miniconda3'

    $PATH_TO_CONDA_INSTALL/bin/conda shell.zsh hook >! $CACHE 2> /dev/null

    if [ ! $? -eq 0 ]
    then
        if [ -f "$PATH_TO_CONDA_INSTALL/etc/profile.d/conda.sh" ]
        then
            . "$PATH_TO_CONDA_INSTALL/etc/profile.d/conda.sh"
        else
            export PATH="$PATH_TO_CONDA_INSTALL/bin:$PATH"
        fi
    fi

    source $CACHE

    # # >>> conda initialize >>>
    # __conda_setup="$($PATH_TO_CONDA_INSTALL/bin/conda 'shell.zsh' 'hook' 2> /dev/null)"
    # if [ $? -eq 0 ]; then
    #     eval "$__conda_setup"
    # else
    #     if [ -f "$PATH_TO_CONDA_INSTALL/etc/profile.d/conda.sh" ]; then
    #         . "$PATH_TO_CONDA_INSTALL/etc/profile.d/conda.sh"
    #     else
    #         export PATH="$PATH_TO_CONDA_INSTALL/bin:$PATH"
    #     fi
    # fi
    # unset __conda_setup
    # # <<< conda initialize <<<

fi

# }}}

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  /usr/local/{bin,sbin}
  $HOME/bin
  $path
)


# Man
# ========================================================================

export MANWIDTH=80  # The width of the text column in the man.

# # Use `bat` as a colorizing pager for `man`
# export MANPAGER="sh -c 'col -bx | bat -l man -p'"


# Less
# ========================================================================

# Set the default Less options.
# Mouse-wheel scrolling has been disabled  -X and -F (exit if the content fits on one screen) to enable it.

# -F: exit if the content fits on one screen
# -g: Highlights just the current match of any searched string.
# -i: Case-insensitive searches.
# -M: Shows more detailed prompt, including file position.
# -N: Shows line numbers (useful for source code viewing).
# -S: Disables line wrap ("chop long lines"). Long lines can be seen by side scrolling.
# -X: Leave file contents on screen when less exits.
# -?: Shows help.
# +F: Follow mode for log.
# -w or --hilite-unread: Temporarily highlights the first "new" line
#    after a forward movement of a full page.
# -z-4: Move page not on the full high of the screen but on high - 4 lines

# export LESS='-F -i -M -R -S -w -z-4'
export LESS='-F -i -M -R -S'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi


# LS_COLORS
# ========================================================================

# ls colors
export LSCOLORS="Gxfxcxdxbxegedabagacad"

# GNU ls colors
export LS_COLORS="di=1;36:ln=35:so=32:pi=33:ex=31:bd=34;46:cd=34;43:su=30;41:sg=30;46:tw=30;42:ow=30;43"


# Bat
# ========================================================================

export BAT_PAGER="less ${LESS}"
# export BAT_PAGER="less -RF"


# Par (paragraph formating)
# ========================================================================

# The following string is recommended in par man.
export PARINIT='rTbgqR B=.,?_A_a Q=_s>|'


# vim: ft=zsh
